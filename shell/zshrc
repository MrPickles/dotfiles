# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

ZSH=$HOME/.oh-my-zsh

# Set zsh theme to be powerlevel10k with patched fontawesome symbols.
POWERLEVEL9K_MODE='nerdfont-complete'
ZSH_THEME='powerlevel10k/powerlevel10k'

# Use a more universal theme for terminals without special font support.
# https://apple.stackexchange.com/a/118500
# https://stackoverflow.com/a/5203740/3344451
_p10k_incompatible_emulators=(
  Apple_Terminal
  vscode
)
if [[ -n "${_p10k_incompatible_emulators[(r)$TERM_PROGRAM]}" ]]; then
  ZSH_THEME='robbyrussell'
fi

plugins=(
  git
  brew
  history-substring-search
  colored-man-pages
  vi-mode
  git-auto-fetch
  fd
  ripgrep
)

# Returns whether the given command is executable or aliased.
_has() {
  return $(whence $1 >/dev/null)
}

# Prepend a directory to path, if it exists and isn't already in the path.
_prepend_to_path() {
  if [ -d $1 -a -z ${path[(r)$1]} ]; then
    path=($1 $path);
  fi
}

# Append a directory to path, if it exists and isn't already in the path.
_append_to_path() {
  if [ -d $1 -a -z ${path[(r)$1]} ]; then
    path=($path $1);
  fi
}

_prepend_to_path /usr/local/bin
_prepend_to_path $HOME/.local/bin

EDITOR='vim'

DISABLE_UPDATE_PROMPT=true

bindkey -v # vi mode for shell
bindkey -e # enable C-x-e for shell editor
# key bindings for history search
bindkey '\e[3~' delete-char
bindkey '^R' history-incremental-search-backward

setopt correct
setopt rmstarsilent # silence rm * confirmation

# Use 256 color for tmux.
alias tmux="TERM=screen-256color-bce tmux"
# Attempt to take over existing sessions before creating a new tmux session.
TMUX_DEFAULT_SESSION="tmux"
alias t="tmux a -d -t ${TMUX_DEFAULT_SESSION} 2> /dev/null || tmux new -s ${TMUX_DEFAULT_SESSION}"

# Use Neovim instead of classic vim (if available)
if _has nvim; then
  alias vim="nvim"
  alias vi="nvim"
fi

# Explicity bind home and end keys (in case of terminal compatibility issues)
bindkey "${terminfo[khome]}" beginning-of-line
bindkey "${terminfo[kend]}" end-of-line

# Decrease delay in entering normal mode in shell.
# https://www.johnhawthorn.com/2012/09/vi-escape-delays/
KEYTIMEOUT=1

if [[ -z "$TMUX" ]]; then
  TERM="xterm-256color"
fi

if _has rg; then
  export RIPGREP_CONFIG_PATH="$HOME/.ripgreprc"
fi

if _has bat; then
  # Export theme for http://github.com/sharkdp/bat.
  export BAT_THEME="TwoDark"
fi

# Configure fzf (if available).
if _has fzf; then
  if _has fd; then
    # Use fd for fzf.
    FZF_DEFAULT_COMMAND='fd --type f --follow --hidden'
    FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    # Use fd for fzf directory search.
    FZF_ALT_C_COMMAND='fd --type d --color never'
  fi

  # Display source tree and file preview for CTRL-T and ALT-C.
  if _has tree; then
    # Show subdir tree for directories.
    FZF_ALT_C_OPTS="--preview '(exa --tree --color=always {} || tree -C {}) | head -200'"
  fi

  # Bind alt-j/k/d/u to moving the preview window for fzf.
  FZF_DEFAULT_OPTS="--bind alt-k:preview-up,alt-j:preview-down,alt-u:preview-page-up,alt-d:preview-page-down"

  # Show previews for files and directories.
  # Having `bat` or `highlight` (or any of the other binaries below) installed
  # enables syntax highlighting.
  FZF_CTRL_T_OPTS="--preview '(bat --style=numbers --color=always {} || highlight -O ansi -l {} || coderay {} || rougify {} || cat {}) 2> /dev/null | head -200'"

  # Some basic fzf-tab configs.
  plugins+=(fzf-tab)
  zstyle ':fzf-tab:complete:cd:*' fzf-preview '(exa --tree --color=always $realpath || tree -C $realpath) 2> /dev/null'
  zstyle ':completion:*:descriptions' format '[%d]'
  zstyle ':fzf-tab:*' switch-group ',' '.'
fi

# Source Oh-My-ZSH. All theme and plugin configs must come beforehand. This may have side-effects.
source $ZSH/oh-my-zsh.sh

# Alias `exa` as default `ls` command (if available).
# This must come after OMZ. (The library overwrites this alias.)
if _has exa; then
  alias ls="exa"
fi

# Source fzf scripts via via local installation.
# OMZ overwrites some of these scripts, so this must come afterwards.
if [ -e ~/.vim/plugged/fzf ]; then
  _append_to_path ~/.vim/plugged/fzf/bin
  # Source fzf key bindings and auto-completion.
  source ~/.vim/plugged/fzf/shell/key-bindings.zsh
  source ~/.vim/plugged/fzf/shell/completion.zsh
fi

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# Source local zshrc configs.
if [[ -f ~/.zshrc.local ]]; then
  source ~/.zshrc.local
fi
